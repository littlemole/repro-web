############################################
cmake_minimum_required(VERSION 2.8.9)
cmake_policy(SET CMP0054 NEW)
project(reproweb)
include(CTest)

set(CMAKE_CXX_STANDARD 14)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

set(INSTALL_DEBUG "On")
set(INSTALL_RELEASE "On")

find_package(GTest REQUIRED)
find_package(OpenSSL REQUIRED)
find_package(ZLIB REQUIRED)

############################################
# clang support
############################################

set(LINKERFLAGS "")
set(STDLIB "")
set(FLAGS "")

if ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
    set(STDLIB "c++abi")
    set(LINKERFLAGS "-stdlib=libc++ -fcoroutines-ts  -std=c++14")
    set(FLAGS "-stdlib=libc++ -fcoroutines-ts -D_RESUMABLE_FUNCTIONS_SUPPORTED")
endif()

set( CMAKE_EXE_LINKER_FLAGS  "${CMAKE_EXE_LINKER_FLAGS} ${LINKERFLAGS}" )


############################################
# OS support
############################################


IF (WIN32)

	set(OSLIBS "Ws2_32" "Rpcrt4")

	find_library(PRIOCPP_LIBRARY NAMES priocpp)
	find_library(PRIOCPPD_LIBRARY NAMES priocppd)
	find_library(PRIOHTTP_LIBRARY NAMES priohttp)
	find_library(PRIOHTTPD_LIBRARY NAMES priohttpd)
	find_library(NG2HTTP_LIBRARY NAMES nghttp2)
	find_library(LIBEVENT_LIBRARY NAMES event)
    find_library(CRYPTONEAT_LIBRARY NAMES cryptoneat)
    find_library(CRYPTONEATD_LIBRARY NAMES cryptoneatd)
	find_library(JSONCPP_LIBRARY NAMES jsoncpp)
    find_library(CURL_LIBRARY NAMES libcurl)
    find_library(REPROCURL_LIBRARY NAMES reprocurl)
    find_library(REPROCURLD_LIBRARY NAMES reprocurld)

	set(BUILDFLAGS "-await ${FLAGS}")
	set(DEBUGFLAGS -DMOL_PROMISE_DEBUG)

	set(LIBS ${JSONCPP_LIBRARY} ${GTEST_LIBRARIES} ${OPENSSL_LIBRARIES} ${NG2HTTP_LIBRARY} ${CURL_LIBRARY} ${ZLIB_LIBRARIES} ${STDLIB} ${OSLIBS})

	if( "${CMAKE_BUILD_TYPE}" STREQUAL "Release")
		set(INSTALL_DEBUG "Off")
		set(LIBS ${CMAKE_PROJECT_NAME} ${PRIOHTTP_LIBRARY} ${REPROCURL_LIBRARY} ${PRIOCPP_LIBRARY} ${CRYPTONEAT_LIBRARY} ${LIBS})
		set(DEBUGFLAGS " ")
	else()
		set(INSTALL_RELEASE "Off")
		set(LIBS ${CMAKE_PROJECT_NAME}d ${PRIOHTTPD_LIBRARY} ${REPROCURLD_LIBRARY} ${PRIOCPPD_LIBRARY} ${CRYPTONEATD_LIBRARY} ${LIBS})
		set(DEBUGFLAGS "-DMOL_PROMISE_DEBUG ")
	endif()


 ELSEIF(UNIX)

    set(OSLIBS "pthread")

    find_package(PkgConfig)
    pkg_check_modules(CURL REQUIRED libcurl)
    pkg_check_modules(NGHTTP2 REQUIRED libnghttp2)
    pkg_check_modules(PRIOCPP REQUIRED priocpp)
    pkg_check_modules(PRIOHTTP REQUIRED priohttp)
    pkg_check_modules(LIBEVENT REQUIRED libevent_pthreads)
    pkg_check_modules(CRYPTONEAT REQUIRED cryptoneat)
    pkg_check_modules(JSONCPP REQUIRED jsoncpp)

	set(LIBEVENT_LIBRARY ${LIBEVENT_LIBRARIES})

    set(BUILDFLAGS " ${FLAGS} ${JSONCPP_CFLAGS} ${PRIOCPP_CFLAGS}")
	set(DEBUGFLAGS "-g -DMOL_PROMISE_DEBUG")

	set(LIBS ${CMAKE_PROJECT_NAME}d priohttpd reprocurld priocppd ${JSONCPP_LIBRARIES} ${GTEST_LIBRARIES} ${OPENSSL_LIBRARIES} ${NGHTTP2_LIBRARIES} ${CRYPTONEAT_LIBRARIES} ${CURL_LIBRARIES} ${ZLIB_LIBRARIES} ${STDLIB} )
ENDIF ()

############################################
# macros
############################################

macro(AddCompilerFlags target flags)
    get_target_property(CF ${target} COMPILE_FLAGS)
    if(CF STREQUAL "CF-NOTFOUND")
        SET(CF "") # set to empty string
    else()
        SET(CF "${CF} ") # a space to cleanly separate from existing content
    endif()

    SET(CF "${CF} ${flags}" )
    set_target_properties(${target} PROPERTIES COMPILE_FLAGS ${CF} )
endmacro()

############################################
# defaults
############################################

set(BACKEND ${LIBEVENT_LIBRARY})

option(WITH_LIBEVENT "use libevent (default:on)" ON)
if(WITH_LIBEVENT)
    add_definitions(-DPROMISE_USE_LIBEVENT)
else()
    add_definitions(-DPROMISE_USE_BOOST_ASIO)
    set(BACKEND "boost_system")
endif()


############################################
# sources
############################################

include_directories(include)
include_directories(/usr/local/include)
include_directories(${OPENSSL_INCLUDE_DIR})

file(GLOB SOURCES "src/*.cpp")
file(GLOB_RECURSE HEADERS "include/${CMAKE_PROJECT_NAME}/*.h")

############################################
# library targets
############################################

add_library(${CMAKE_PROJECT_NAME}  STATIC ${SOURCES})
add_library(${CMAKE_PROJECT_NAME}d STATIC ${SOURCES})


AddCompilerFlags(${CMAKE_PROJECT_NAME} ${BUILDFLAGS})

AddCompilerFlags(${CMAKE_PROJECT_NAME}d ${BUILDFLAGS})
AddCompilerFlags(${CMAKE_PROJECT_NAME}d ${DEBUGFLAGS})

############################################
# tests
############################################

add_executable(Tests t/test.cpp)

AddCompilerFlags(Tests ${BUILDFLAGS})
AddCompilerFlags(Tests ${DEBUGFLAGS})

target_link_libraries(Tests ${LIBS} ${BACKEND} )

add_test(NAME AllTests COMMAND Tests)

file(COPY pem DESTINATION .)
file(COPY config.json DESTINATION .)
 
############################################
# install rules
############################################
 
if( "${INSTALL_RELEASE}" STREQUAL "On")
	install(TARGETS ${CMAKE_PROJECT_NAME} DESTINATION lib)
endif()

if( "${INSTALL_DEBUG}" STREQUAL "On")
	install(TARGETS ${CMAKE_PROJECT_NAME}d DESTINATION lib)
endif()

INSTALL( DIRECTORY include/${CMAKE_PROJECT_NAME} DESTINATION include )


install(FILES ${CMAKE_PROJECT_NAME}.pc DESTINATION lib/pkgconfig)

